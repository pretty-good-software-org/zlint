//! CLI options. Not necessarily linting-specific.

/// Any number of warnings should cause zlint to exit with a non-zero status
/// code.
deny_warnings: bool = false,
/// Enable verbose logging.
verbose: bool = false,
/// Print version and exit.
version: bool = false,
/// Only display errors. Warnings are counted but not shown.
quiet: bool = false,
/// Instead of linting a file, print its AST as JSON to stdout.
///
/// This is primarily for debugging purposes.
print_ast: bool = false,
/// Instead of linting a file, print its control flow graph as Graphviz DOT
/// to stdout.
///
/// This is primarily for debugging purposes.
print_cfg: bool = false,
/// Include top-level decl initializer containers in `--print-cfg` output.
cfg_decls: bool = false,
/// How diagnostics are formatted.
format: formatter.Kind = .graphical,
/// Print a summary about # of warnings and errors. Only applies for some formats.
summary: bool = true,
/// Instead of walking directories in cwd, read names of files to lint from stdin.
/// If relative, paths are resolved from the cwd.
stdin: bool = false,
/// enable auto fixes
fix: bool = false,
/// Like `--fix`, but also enable potentially dangerous fixes.
fix_dangerously: bool = false,
/// Positional arguments
args: std.ArrayList([]const u8) = .empty,

pub const usage =
    \\Usage: zlint [options] [<dirs>]
    \\       zlint update
;
const help =
    \\Commands:
    \\  update             Download and install the latest stable release
    \\
    \\Options:
    \\--print-ast <file>  Parse a file and print its AST as JSON
    \\--print-cfg <file>  Analyze a file and print its control flow graph as Graphviz DOT
    \\--cfg-decls         Include top-level decl initializers in --print-cfg output
    \\-f, --format <fmt>  Choose an output format (default, graphical, ascii, json, github, gh)
    \\--no-summary        Do not print a summary after linting
    \\-S, --stdin         Lint filepaths received from stdin (newline separated)
    \\--fix               Apply automatic fixes where possible
    \\--fix-dangerously   Like --fix, but also enable potentially dangerous fixes
    \\--deny-warnings     Warnings produce a non-zero exit code
    \\-q, --quiet         Only display error diagnostics
    \\-V, --verbose       Enable verbose logging   
    \\-v, --version       Print version and exit
    \\-h, --help          Show this help message
;
const ParseError = error{
    OutOfMemory,
    InvalidArg,
    InvalidArgValue,
    WriterError,
    WriteFailed,
};
pub fn parseArgv(alloc: Allocator, io: std.Io, args: std.process.Args, err: ?*Error) ParseError!Options {
    // NOTE: on most platforms this does not actually allocate memory; only
    // Windows and WASI need the allocator to decode the command line.
    var argv = try std.process.Args.Iterator.initAllocator(args, alloc);
    defer argv.deinit();
    return parse(alloc, io, argv, err);
}

fn parse(alloc: Allocator, io: std.Io, args_iter: anytype, err: ?*Error) ParseError!Options {
    var argv = args_iter;
    var opts = Options{};
    errdefer opts.deinit(alloc);

    // skip binary name
    _ = argv.next() orelse return opts;
    while (argv.next()) |arg| {
        if (arg.len == 0) continue;
        if (arg[0] != '-') {
            try opts.args.append(alloc, arg);
            continue;
        }
        if (eq(arg, "--fix")) {
            opts.fix = true;
        } else if (eq(arg, "--fix-dangerously")) {
            opts.fix_dangerously = true;
            opts.fix = true;
        } else if (eq(arg, "-q") or eq(arg, "--quiet")) {
            opts.quiet = true;
        } else if (eq(arg, "-V") or eq(arg, "--verbose")) {
            opts.verbose = true;
        } else if (eq(arg, "-v") or eq(arg, "--version")) {
            opts.version = true;
        } else if (eq(arg, "--deny-warnings")) {
            opts.deny_warnings = true;
        } else if (eq(arg, "-S") or eq(arg, "--stdin")) {
            opts.stdin = true;
        } else if (eq(arg, "-f") or eq(arg, "--format")) {
            // TODO: comptime string concat on format names
            const fmt = argv.next() orelse {
                if (err) |e| {
                    e.* = Error.fmt(alloc, "Invalid format name: {s}. Valid names are {s}.", .{ arg, FORMAT_NAMES }) catch @panic("OOM");
                }
                return error.InvalidArg;
            };
            opts.format = formatter.Kind.fromString(fmt) orelse {
                if (err) |e| {
                    e.* = Error.fmt(alloc, "Invalid format name: {s}. Valid names are {s}.", .{ arg, FORMAT_NAMES }) catch @panic("OOM");
                }
                return error.InvalidArgValue;
            };
        } else if (eq(arg, "--no-summary")) {
            opts.summary = false;
        } else if (eq(arg, "--print-ast")) {
            opts.print_ast = true;
        } else if (eq(arg, "--print-cfg")) {
            opts.print_cfg = true;
        } else if (eq(arg, "--cfg-decls")) {
            opts.cfg_decls = true;
        } else if (eq(arg, "-h") or eq(arg, "--help") or eq(arg, "--hlep") or eq(arg, "-help")) {
            var buf: [512]u8 = undefined;
            var writer = std.Io.File.stdout().writer(io, &buf);
            var stdout = &writer.interface;
            defer stdout.flush() catch @panic("failed to flush writer");

            try stdout.writeAll(usage);
            try stdout.writeAll(util.NEWLINE ++ util.NEWLINE);
            try stdout.writeAll(help);
            try stdout.writeAll(util.NEWLINE);
            std.process.exit(0);
        } else if (eq(arg, "--")) {
            continue;
        } else {
            if (err) |e| {
                e.* = Error.fmt(alloc, "unknown option: {s}\n", .{arg}) catch @panic("OOM");
            }
            return error.InvalidArg;
        }
    }

    return opts;
}

pub fn deinit(self: *Options, alloc: std.mem.Allocator) void {
    self.args.deinit(alloc);
}

inline fn eq(arg: anytype, name: @TypeOf(arg)) bool {
    return std.mem.eql(u8, arg, name);
}
// TODO: comptime string concat on format names
const FORMAT_NAMES: []const u8 = "default, graphical, ascii, json, github, gh";

const Options = @This();
const std = @import("std");
const util = @import("util");
const Allocator = std.mem.Allocator;
const formatter = @import("zlint").report.formatter;
const Error = @import("zlint").Error;

const t = std.testing;

test parse {
    const List = std.ArrayListUnmanaged([]const u8);
    const Case = struct { []const u8, Options };

    const src_list: List = brk: {
        const items = &[_][]const u8{"src"};
        break :brk List{ .items = @constCast(items), .capacity = items.len };
    };
    const src_test_list: List = brk: {
        const items = &[_][]const u8{ "src", "test" };
        break :brk List{ .items = @constCast(items), .capacity = items.len };
    };

    const test_cases = [_]Case{
        .{ "", .{} },
        .{ "zlint", .{} },
        .{ "zlint --", .{} },
        .{ "zlint --print-ast", .{ .print_ast = true } },
        .{ "zlint --print-cfg", .{ .print_cfg = true } },
        .{ "zlint --cfg-decls", .{ .cfg_decls = true } },
        .{ "zlint --print-cfg --cfg-decls", .{ .print_cfg = true, .cfg_decls = true } },
        .{ "zlint --print-cfg src", .{ .print_cfg = true, .args = src_list } },
        .{ "zlint --fix", .{ .fix = true } },
        .{ "zlint --no-summary", .{ .summary = false } },
        .{ "zlint --verbose", .{ .verbose = true } },
        .{ "zlint -V", .{ .verbose = true } },
        .{ "zlint --verbose --print-ast", .{ .verbose = true, .print_ast = true } },
        .{ "zlint src -V", .{ .verbose = true, .args = src_list } },
        .{ "zlint -V src", .{ .verbose = true, .args = src_list } },
        .{ "zlint -V -- src", .{ .verbose = true, .args = src_list } },
        .{ "zlint -V src test ", .{ .verbose = true, .args = src_test_list } },
        .{ "zlint -V -- src test ", .{ .verbose = true, .args = src_test_list } },
        .{ "zlint src -V test", .{ .verbose = true, .args = src_test_list } },
    };

    for (test_cases) |test_case| {
        const argv = std.mem.splitScalar(u8, test_case[0], ' ');
        const expected: Options = test_case[1];
        var opts = try parse(t.allocator, t.io, argv, null);
        defer opts.deinit(t.allocator);

        // Reflective so a newly added option is covered the moment it exists.
        inline for (@typeInfo(Options).@"struct".fields) |field| {
            if (comptime std.mem.eql(u8, field.name, "args")) continue;
            t.expectEqual(@field(expected, field.name), @field(opts, field.name)) catch |e| {
                std.debug.print("^ field '{s}' of `{s}`\n", .{ field.name, test_case[0] });
                return e;
            };
        }

        try t.expectEqual(expected.args.items.len, opts.args.items.len);
        for (0..expected.args.items.len) |i| {
            try t.expectEqualStrings(
                expected.args.items[i],
                opts.args.items[i],
            );
        }
    }
}

test "invalid --format" {
    var err: Error = undefined;
    const argv = std.mem.splitScalar(u8, "zlint --format this-is-not-a-valid-format", ' ');
    defer err.deinit(t.allocator);
    try t.expectError(
        error.InvalidArgValue,
        parse(t.allocator, t.io, argv, &err),
    );
    try t.expect(std.mem.indexOf(u8, err.message.borrow(), "Invalid format name") != null);
}

test "ascii format" {
    const argv = std.mem.splitScalar(u8, "zlint --format ascii", ' ');
    var opts = try parse(t.allocator, t.io, argv, null);
    defer opts.deinit(t.allocator);
    try t.expectEqual(formatter.Kind.ascii, opts.format);
}
