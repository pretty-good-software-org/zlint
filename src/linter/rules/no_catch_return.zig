//! ## What This Rule Does
//! Disallows `catch` blocks that immediately return the caught error.
//!
//! Catch blocks that do nothing but return their error can and should be
//! replaced with a `try` statement. This rule allows for `catch`es that
//! have side effects such as printing the error or switching over it.
//!
//! ## Examples
//!
//! Examples of **incorrect** code for this rule:
//! ```zig
//! fn foo() !void {
//!   riskyOp() catch |e| return e;
//!   riskyOp() catch |e| { return e; };
//! }
//! ```
//!
//! Examples of **correct** code for this rule:
//! ```zig
//! const std = @import("std");
//!
//! fn foo() !void{
//!   try riskyOp();
//! }
//!
//! // re-throwing with side effects is fine
//! fn bar() !void {
//!   riskyOp() catch |e| {
//!     std.debug.print("Error: {any}\n", .{e});
//!     return e;
//!   };
//! }
//!
//! // throwing a new error is fine
//! fn baz() !void {
//!   riskyOp() catch |e| return error.OutOfMemory;
//! }
//! ```

const std = @import("std");
const util = @import("util");
const Semantic = @import("../../Semantic.zig");
const _rule = @import("../rule.zig");

const Ast = Semantic.Ast;
const Node = Ast.Node;
const Token = Semantic.Token;
const TokenIndex = Ast.TokenIndex;
const LinterContext = @import("../lint_context.zig");
const Rule = _rule.Rule;
const NodeWrapper = _rule.NodeWrapper;
const Error = @import("../../Error.zig");
const Cow = util.Cow(false);
const Fix = @import("../fix.zig").Fix;

// Rule metadata
const NoCatchReturn = @This();
pub const meta: Rule.Meta = .{
    .name = "no-catch-return",
    .category = .pedantic,
    .default = .warning,
    .fix = Fix.Meta.safe_fix,
};

fn noCatchReturnDiagnostic(ctx: *LinterContext, return_node: Node.Index) Error {
    var err = ctx.diagnostic(
        "Caught error is immediately returned",
        .{ctx.spanN(return_node)},
    );
    err.help = Cow.static("Use a `try` statement to return unhandled errors.");
    return err;
}

// Runs on each node in the AST. Useful for syntax-based rules.
pub fn runOnNode(_: *const NoCatchReturn, wrapper: NodeWrapper, ctx: *LinterContext) void {
    const ast = ctx.ast();
    const tok_tags: []const Token.Tag = ast.tokens.items(.tag);
    const node = wrapper.node;

    if (node.tag != .@"catch") return;

    // .@"catch" data is .node_and_node: [0]=operand, [1]=fallback
    const catch_data = node.data.node_and_node;
    if (catch_data[1] == .root) return; // NOTE: in v0.15, this is non-optional. Remove?
    var return_node: Node.Index = catch_data[1];
    const end_of_last_tok = ctx.semantic.tokenSpan(ast.lastToken(return_node)).end;

    // look for a return statement. We loop to handle single-statement blocks.
    while (true) {
        switch (ast.nodeTag(return_node)) {
            .@"return" => break,
            .block_two, .block_two_semicolon => {
                // .block_two data is .opt_node_and_opt_node
                const blk_data = ast.nodeData(return_node).opt_node_and_opt_node;
                const first = blk_data[0].unwrap();
                const second = blk_data[1].unwrap();
                // we're looking for only a single statement in the block.
                if (first == null or second != null) return;
                return_node = first.?;
                continue;
            },
            // guaranteed to have more than one statement
            .block, .block_semicolon => return,
            else => return,
        }
    }

    // only check catches that bind an error payload, e.g. `catch |e|`
    var ident_tok: TokenIndex = node.main_token + 1;
    if (tok_tags[ident_tok] != .pipe) return else ident_tok += 1;
    if (tok_tags[ident_tok] == .asterisk) ident_tok += 1;
    if (tok_tags[ident_tok] != .identifier) return;

    // .@"return" data is .opt_node
    const return_param = ast.nodeData(return_node).opt_node.unwrap() orelse return;
    if (ast.nodeTag(return_param) != .identifier) return;

    // todo: add symbols to node links
    const error_param = ctx.semantic.tokenSlice(ident_tok);
    const returned_ident = ast.getNodeSource(return_param);
    if (std.mem.eql(u8, error_param, returned_ident)) {
        ctx.reportWithFix(
            Ctx{
                .catch_node = wrapper.idx,
                .tried_expr = catch_data[0],
                .end = end_of_last_tok,
            },
            noCatchReturnDiagnostic(ctx, return_node),
            &replaceWithTry,
        );
    }
}

const Ctx = struct {
    catch_node: Node.Index,
    tried_expr: Node.Index,
    end: u32,
};

fn replaceWithTry(ctx: Ctx, builder: Fix.Builder) !Fix {
    const catch_node = ctx.catch_node;
    const tried_expr = ctx.tried_expr;
    var span = builder.spanCovering(.node, @intFromEnum(catch_node));
    span.end = ctx.end;

    return builder.replacef(
        span,
        "try {s}",
        .{builder.snippet(.node, @intFromEnum(tried_expr))},
    );
}

// Used by the Linter to register the rule so it can be run.
pub fn rule(self: *NoCatchReturn) Rule {
    return Rule.init(self);
}

const RuleTester = @import("../tester.zig");
test NoCatchReturn {
    const t = std.testing;

    var no_catch_return = NoCatchReturn{};
    var runner = RuleTester.init(t.allocator, no_catch_return.rule());
    defer runner.deinit();

    const pass = &[_][:0]const u8{
        "fn bar() !u32 { return 1; }\nfn foo() !u32 { try bar(); return 1; }",
        \\const std = @import("std");
        \\fn bar() !u32 { return 1; }
        \\fn foo() !u32 {
        \\  const x = bar catch |e| {
        \\    std.debug.print("Error: {any}\n", .{e});
        \\    return e;
        \\  };
        \\  return x;
        \\}
        \\const std = @import("std");
        \\fn bar() !void {}
        \\fn foo() !void {
        \\  bar() catch |e| return error.OutOfMemory;
        \\}
    };

    const fail = &[_][:0]const u8{
        \\fn bar() !void { }
        \\fn foo() !void {
        \\  bar() catch |e| return e;
        \\}
        ,
        \\fn bar() !void { }
        \\fn foo() !void {
        \\  bar() catch |e| {
        \\    return e;
        \\  };
        \\}
        ,
        \\fn bar() !void { }
        \\fn foo() !void {
        \\  bar() catch |e| {
        \\    // comments won't save you
        \\    return e;
        \\  };
        \\}
        ,
    };

    const fix = &[_]RuleTester.FixCase{
        .{
            .src =
            \\fn foo() !void {
            \\  bar() catch |e| return e;
            \\}
            ,
            .expected =
            \\fn foo() !void {
            \\  try bar();
            \\}
            ,
        },
        .{
            .src =
            \\fn foo() !void {
            \\  bar() catch |e| {
            \\    return e;
            \\  };
            \\}
            ,
            .expected =
            \\fn foo() !void {
            \\  try bar();
            \\}
            ,
        },
    };

    try runner
        .withPass(pass)
        .withFail(fail)
        .withFix(fix)
        .run();
}
