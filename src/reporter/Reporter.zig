pub const Options = struct {
    quiet: bool = false,
    report_stats: bool = true,
};

pub const Reporter = struct {
    opts: Options = .{},
    stats: Stats = .{},

    io: std.Io,
    writer: *io.Writer,
    writer_lock: Mutex = .init,

    alloc: Allocator,
    /// pointer to formatter impl. Allocation is owned.
    ptr: *anyopaque,
    vtable: struct {
        format: *const fn (ctx: *anyopaque, writer: *io.Writer, e: Error) FormatError!void,
        deinit: *const fn (ctx: *anyopaque, allocator: Allocator) void,
        destroy: *const fn (ctx: *anyopaque, allocator: Allocator) void,
    },

    /// Shorthand for creating a `Reporter` with a `GraphicalFormatter`, since
    /// this is so common.
    pub fn graphical(
        io_: std.Io,
        writer: *io.Writer,
        allocator: Allocator,
        // Optionally override the default theme
        theme: ?formatters.Graphical.Theme,
    ) Allocator.Error!Reporter {
        var formatter = formatters.Graphical{ .alloc = allocator };
        if (theme) |t| formatter.theme = t;
        return init(formatters.Graphical, formatter, io_, writer, allocator);
    }

    /// Create a reporter for `kind` using a file-backed writer.
    pub fn initKind(kind: formatters.Kind, io_: std.Io, environ: std.process.Environ, writer: *io.File.Writer, allocator: Allocator) Allocator.Error!Reporter {
        switch (kind) {
            .ascii => {
                const color = !util.env.checkEnvFlag(environ, "NO_COLOR", .enabled);
                const f = formatters.Graphical.ascii(allocator, color);
                return init(formatters.Graphical, f, io_, &writer.interface, allocator);
            },
            .graphical => {
                // TODO: any non-falsy value should turn off colors
                const color = !util.env.checkEnvFlag(environ, "NO_COLOR", .enabled);
                const f = if (tty.supportsUnicode(io_, writer.file, environ))
                    formatters.Graphical.unicode(allocator, color)
                else
                    formatters.Graphical.ascii(allocator, color);
                return init(formatters.Graphical, f, io_, &writer.interface, allocator);
            },
            .github => {
                const f = formatters.Github{};
                return init(formatters.Github, f, io_, &writer.interface, allocator);
            },
            .json => {
                const f = formatters.JSON{};
                return init(formatters.JSON, f, io_, &writer.interface, allocator);
            },
        }
    }

    /// Create a new reporter. `formatter` is moved.
    pub fn init(
        comptime Formatter: type,
        formatter: Formatter,
        io_: std.Io,
        writer: *io.Writer,
        allocator: Allocator,
    ) Allocator.Error!Reporter {
        comptime if (!@hasDecl(Formatter, "meta")) {
            @compileError(@typeName(Formatter) ++ " is missing a meta: formatter.Meta declaration.");
        };

        const fmt = try allocator.create(Formatter);
        fmt.* = formatter;
        const meta: formatters.Meta = Formatter.meta;

        const gen = struct {
            fn format(ctx: *anyopaque, _writer: *io.Writer, e: Error) FormatError!void {
                const this: *Formatter = @ptrCast(@alignCast(ctx));
                return Formatter.format(this, _writer, e);
            }
            fn deinit(ctx: *anyopaque, alloc: Allocator) void {
                if (!@hasDecl(Formatter, "deinit")) return;
                const this: *Formatter = @ptrCast(@alignCast(ctx));
                const info = @typeInfo(@TypeOf(Formatter.deinit));
                switch (info.@"fn".params.len) {
                    1 => this.deinit(),
                    2 => this.deinit(alloc),
                    else => @compileError("Formatter.deinit must take (this) or (this, allocator) as parameters."),
                }
            }
            fn destroy(ctx: *anyopaque, alloc: Allocator) void {
                const this: *Formatter = @ptrCast(@alignCast(ctx));
                alloc.destroy(this);
            }
        };

        return .{
            .io = io_,
            .writer = writer,
            .opts = .{
                .report_stats = meta.report_statistics,
            },
            .alloc = allocator,
            .ptr = @ptrCast(fmt),
            .vtable = .{
                .format = &gen.format,
                .deinit = &gen.deinit,
                .destroy = &gen.destroy,
            },
        };
    }

    pub fn reportErrors(self: *Reporter, errors: std.array_list.Managed(Error)) Allocator.Error!void {
        defer errors.deinit();
        try self.reportErrorSlice(errors.allocator, errors.items);
    }

    pub fn reportErrorSlice(self: *Reporter, alloc: std.mem.Allocator, errors: []Error) Allocator.Error!void {
        self.stats.recordErrors(errors);
        if (errors.len == 0) return;

        var stackalloc = std.heap.stackFallback(1024, alloc);
        const allocator = stackalloc.get();

        var w = try std.Io.Writer.Allocating.initCapacity(allocator, 256);
        defer w.deinit();

        for (errors) |err| {
            var e = err;
            defer e.deinit(alloc);
            if (self.opts.quiet and err.severity != .err) continue;
            self.vtable.format(self.ptr, &w.writer, err) catch |fmt_err| {
                std.debug.panic("Failed to write error: {any}", .{fmt_err});
            };
            w.writer.writeByte('\n') catch @panic("failed to write newline.");
        }

        w.writer.flush() catch @panic("failed to flush writer");
        self.writer_lock.lockUncancelable(self.io);
        defer self.writer_lock.unlock(self.io);
        var written = w.toArrayList();
        defer written.deinit(allocator);
        _ = self.writer.writeAll(written.items) catch @panic("failed to write diagnostics to buffer");
        self.writer.flush() catch @panic("failed to flush writer");
    }

    pub fn printStats(self: *Reporter, duration: i64) void {
        if (!self.opts.report_stats) return;
        const yellow, const yd = comptime blk: {
            var c = Chameleon.initComptime();
            const yellow = c.yellow().createPreset();
            // Yellow {d} format string
            const yd = yellow.open ++ "{d}" ++ yellow.close;
            break :blk .{ yellow, yd };
        };

        const errors = self.stats.numErrorsSync();
        const warnings = self.stats.numWarningsSync();
        const files = self.stats.numFilesSync();
        self.writer.print(
            "\tFound " ++ yd ++ " errors and " ++ yd ++ " warnings across " ++ yd ++ " files in " ++ yellow.open ++ "{d}ms" ++ yellow.close ++ ".\n",
            .{ errors, warnings, files, duration },
        ) catch {};
    }

    /// Deinitialize the underlying formatter. Only frees memory if the reporter
    /// owns this formatter.
    /// 1. The formatter has a `deinit()` method
    /// 2. This reporter owns the formatter.
    pub fn deinit(self: *Reporter) void {
        self.writer_lock.lockUncancelable(self.io);
        defer self.writer_lock.unlock(self.io);
        self.writer.flush() catch |e| std.debug.panic("Reporter failed to flush writer: {s}", .{@errorName(e)});
        self.vtable.deinit(self.ptr, self.alloc);
        self.vtable.destroy(self.ptr, self.alloc);
        self.writer = undefined;

        if (comptime util.IS_DEBUG) {
            self.vtable.format = &PanicFormatter.format;
            self.vtable.deinit = &PanicFormatter.deinit;
        }
    }
};

/// Formatter that always panics. Used to check for use-after-free bugs.
///
/// Only used in debug builds.
const PanicFormatter = struct {
    fn format(_: *anyopaque, _: *io.Writer, _: Error) FormatError!void {
        std.debug.panic("Attempted to format an error after this Reporter was freed.", .{});
    }
    fn deinit(_: *anyopaque, _: Allocator) void {
        std.debug.panic("Attempted to deinitialize the same Reporter twice. This is a bug.", .{});
    }
};

const Stats = struct {
    num_files: AtomicUsize = .init(0),
    num_errors: AtomicUsize = .init(0),
    num_warnings: AtomicUsize = .init(0),

    pub fn recordErrors(self: *Stats, errors: []const Error) void {
        var num_warnings: usize = 0;
        var num_errors: usize = 0;
        for (errors) |err| {
            switch (err.severity) {
                .warning => num_warnings += 1,
                .err => num_errors += 1,
                else => {},
            }
        }
        _ = self.num_files.fetchAdd(1, .acquire);
        _ = self.num_errors.fetchAdd(num_errors, .acquire);
        _ = self.num_warnings.fetchAdd(num_warnings, .acquire);
    }

    pub fn recordSuccess(self: *Stats) void {
        _ = self.num_files.fetchAdd(1, .acquire);
    }

    /// Get the number of linted files. Only call this after all files have been
    /// processed.
    pub fn numFilesSync(self: *const Stats) usize {
        return self.num_files.raw;
    }

    /// Get the number of lint errors. Only call this after all files have been
    /// processed.
    pub fn numErrorsSync(self: *const Stats) usize {
        return self.num_errors.raw;
    }

    /// Get the number of lint warnings. Only call this after all files have been
    /// processed.
    pub fn numWarningsSync(self: *const Stats) usize {
        return self.num_warnings.raw;
    }
};

const std = @import("std");
const io = std.Io;
const util = @import("util");
const tty = @import("../io/tty.zig");
const formatters = @import("./formatter.zig");
const Chameleon = @import("chameleon");
const Error = @import("../Error.zig");
const Allocator = std.mem.Allocator;
const FormatError = formatters.FormatError;

const AtomicUsize = std.atomic.Value(usize);
const Mutex = std.Io.Mutex;

test {
    std.testing.refAllDecls(@This());
}

test "graphical format checks the output destination for unicode support" {
    const MockIo = struct {
        fn fileIsTty(_: ?*anyopaque, file: io.File) io.Cancelable!bool {
            return std.meta.eql(file, io.File.stderr());
        }
    };

    var vtable = std.testing.io.vtable.*;
    vtable.fileIsTty = MockIo.fileIsTty;
    const test_io: io = .{ .userdata = null, .vtable = &vtable };

    var stderr_writer = io.File.stderr().writer(test_io, &.{});

    var stderr_reporter = try Reporter.initKind(
        .graphical,
        test_io,
        .empty,
        &stderr_writer,
        std.testing.allocator,
    );
    defer stderr_reporter.deinit();

    const stderr_formatter: *formatters.Graphical = @ptrCast(@alignCast(stderr_reporter.ptr));
    try std.testing.expectEqualStrings("|", stderr_formatter.theme.characters.vbar);

    var stdout_writer = io.File.stdout().writer(test_io, &.{});
    var stdout_reporter = try Reporter.initKind(
        .graphical,
        test_io,
        .empty,
        &stdout_writer,
        std.testing.allocator,
    );
    defer stdout_reporter.deinit();

    const stdout_formatter: *formatters.Graphical = @ptrCast(@alignCast(stdout_reporter.ptr));
    try std.testing.expectEqualStrings("│", stdout_formatter.theme.characters.vbar);
}
