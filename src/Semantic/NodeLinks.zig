//! Links AST nodes to other semantic data

const NodeLinks = @This();

/// Map of AST nodes to their parents. Index is the child node id.
///
/// Confusingly, the root node id is also used as the "null" node id, so the
/// root node technically uses itself as its parent (`parents[0] == 0`). Prefer
/// `getParent` if you need to disambiguate between the root node and the null
/// node.
///
/// Do not insert into this list directly; use `setParent` instead. This method
/// upholds link invariants.
///
/// ### Invariants:
/// - No node is its own parent
/// - No node is the parent of the root node (0 in this case means `null`).
parents: NodeMap(NodeIndex),
/// Map AST nodes to the scope they are in. Index is the node id.
///
/// This is _not_ a mapping for scopes that nodes create.
scopes: NodeMap(Scope.Id),
/// Maps identifier tokens to the symbols bound to them.
///
/// These are the same as `symbol.identifier`, but allow for lookups the other
/// way.
symbols: TokenMap(Symbol.Id),
/// Maps tokens (usually `.identifier`s) to the references they create. Since
/// references are sparse in an AST, a hashmap is used to avoid wasting memory.
references: TokenMap(Reference.Id),
/// Maps AST nodes to the basic block they were lowered into. Only allocated
/// when CFG construction is enabled; empty otherwise.
blocks: NodeMap(Cfg.BasicBlock.Id.Optional),

fn NodeMap(T: type) type {
    return std.ArrayList(T);
}

fn TokenMap(T: type) type {
    return std.AutoHashMapUnmanaged(Ast.TokenIndex, T);
}

pub const empty: NodeLinks = .{
    .parents = .empty,
    .scopes = .empty,
    .symbols = .empty,
    .references = .empty,
    .blocks = .empty,
};

pub fn init(alloc: Allocator, ast: *const Ast) Allocator.Error!NodeLinks {
    var links: NodeLinks = .empty;
    const node_count = ast.nodes.len;

    try links.parents.ensureTotalCapacityPrecise(alloc, node_count);
    links.parents.appendNTimesAssumeCapacity(.root, node_count);
    try links.scopes.ensureTotalCapacityPrecise(alloc, node_count);
    links.scopes.appendNTimesAssumeCapacity(.root, node_count);

    try links.references.ensureTotalCapacity(alloc, 16);

    return links;
}

pub fn deinit(self: *NodeLinks, alloc: Allocator) void {
    inline for (.{ "parents", "scopes", "symbols", "references", "blocks" }) |name| {
        @field(self, name).deinit(alloc);
    }
}

pub inline fn setScope(self: *NodeLinks, node_id: NodeIndex, scope_id: Scope.Id) void {
    const idx = @intFromEnum(node_id);
    assert(
        idx < self.scopes.items.len,
        "Node id out of bounds (id {d} >= {d})",
        .{ idx, self.scopes.items.len },
    );

    self.scopes.items[idx] = scope_id;
}

pub inline fn getScope(self: *const NodeLinks, node_id: NodeIndex) ?Scope.Id {
    if (node_id == .root) {
        @branchHint(.cold);
        return null;
    }
    const idx = @intFromEnum(node_id);
    assert(
        idx < self.scopes.items.len,
        "Node id out of bounds (id {d} >= {d})",
        .{ idx, self.scopes.items.len },
    );
    const scope_id = self.scopes.items[idx];
    return if (scope_id == .root) null else scope_id;
}

pub inline fn setParent(self: *NodeLinks, child_id: NodeIndex, parent_id: NodeIndex) void {
    assert(child_id != parent_id, "AST nodes cannot be children of themselves", .{});
    assert(child_id != .root, "Re-assigning the root node's parent is illegal behavior", .{});
    const parent_idx = @intFromEnum(parent_id);
    assert(
        parent_idx < self.parents.items.len,
        "Parent node id out of bounds (id {d} >= {d})",
        .{ parent_idx, self.parents.items.len },
    );

    self.parents.items[@intFromEnum(child_id)] = parent_id;
}

pub inline fn getParent(self: *const NodeLinks, node_id: NodeIndex) ?NodeIndex {
    if (node_id == .root) {
        return null;
    }
    return self.parents.items[@intFromEnum(node_id)];
}

/// Iterate over a node's parents. The first element is the node itself, and
/// the last will be the root node.
pub fn iterParentIds(self: *const NodeLinks, node_id: NodeIndex) ParentIdsIterator {
    return ParentIdsIterator{ .links = self, .curr_id = node_id };
}

const ParentIdsIterator = struct {
    links: *const NodeLinks,
    curr_id: ?NodeIndex,

    pub fn next(self: *ParentIdsIterator) ?NodeIndex {
        const curr_id = self.curr_id orelse return null;
        // NOTE: using getParent instead of direct _parents access to ensure
        // root node is yielded.
        defer self.curr_id = self.links.getParent(curr_id);
        return self.curr_id;
    }
};

const std = @import("std");
const _ast = @import("ast.zig");
const util = @import("util");

const Ast = _ast.Ast;
const NodeIndex = _ast.NodeIndex;
const Cfg = @import("Cfg.zig");
const Semantic = @import("../Semantic.zig");
const Scope = Semantic.Scope;
const Symbol = Semantic.Symbol;
const Reference = Semantic.Reference;

const Allocator = std.mem.Allocator;
const assert = util.assert;
