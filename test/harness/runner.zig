const std = @import("std");
const builtin = @import("builtin");
const utils = @import("../utils.zig");
const Config = @import("Config.zig");
const TestSuite = @import("TestSuite.zig");

const Allocator = std.mem.Allocator;
const assert = std.debug.assert;
const panic = std.debug.panic;
const print = std.debug.print;

const TestAllocator = std.heap.DebugAllocator(.{
    // Stack unwinding is really expensive. Turning them off results in ~4x speedup.
    // Comment out the line below to debug.
    .stack_trace_frames = 0,
});

const is_debug = builtin.mode == .Debug;
var debug_allocator = TestAllocator{};
var global_runner_instance = TestRunner.new(if (is_debug)
    debug_allocator.allocator()
else
    std.heap.smp_allocator);

var io_threaded: std.Io.Threaded = undefined;
var io_initialized = false;

/// The `Io` instance shared by all e2e test suites. Only valid after
/// `TestRunner.runAll` has started.
pub fn io() std.Io {
    std.debug.assert(io_initialized);
    return io_threaded.io();
}

/// Harness-wide settings (tty, color). Only meaningful once `runAll` has
/// started; see `Config`.
pub const config = Config.get;

pub fn getRunner() *TestRunner {
    return &global_runner_instance;
}

pub fn addTest(test_file: TestRunner.TestFile) *TestRunner {
    assert(global_runner_instance != null);
    return global_runner_instance.?.addTest(test_file);
}

pub fn globalShutdown() void {
    getRunner().deinit();

    if (io_initialized) {
        io_threaded.deinit();
        io_initialized = false;
    }

    if (is_debug) {
        _ = debug_allocator.detectLeaks();
        const status = debug_allocator.deinit();
        if (status == .leak) {
            panic("Memory leak detected\n", .{});
        }
    }
}

pub const TestRunner = struct {
    tests: std.ArrayListUnmanaged(TestFile) = .empty,
    alloc: Allocator,
    config: Config,
    /// Used for `PATH` when spawning subprocesses.
    environ: std.process.Environ = .empty,

    pub inline fn new(alloc: Allocator) TestRunner {
        return TestRunner{ .alloc = alloc, .config = .{} };
    }

    pub inline fn deinit(self: *TestRunner) void {
        for (self.tests.items) |test_file| {
            if (test_file.deinit) |deinit_fn| {
                deinit_fn(self.alloc);
            }
        }
        self.tests.deinit(self.alloc);
    }
    pub inline fn setConfig(self: *TestRunner, cfg: Config) *TestRunner {
        self.config = cfg;
        return self;
    }

    pub inline fn setEnviron(self: *TestRunner, environ: std.process.Environ) *TestRunner {
        self.environ = environ;
        return self;
    }

    pub inline fn addTest(self: *TestRunner, test_file: TestFile) *TestRunner {
        self.tests.append(self.alloc, test_file) catch |e| panic("Failed to add test {s}: {any}\n", .{ test_file.name, e });
        return self;
    }

    pub inline fn runAll(self: *TestRunner) !void {
        if (!io_initialized) {
            io_threaded = .init(self.alloc, .{ .environ = self.environ });
            io_initialized = true;
        }
        try utils.TestFolders.globalInit(io());

        var last_error: ?anyerror = null;
        for (self.tests.items) |test_file| {
            if (test_file.globalSetup) |global_setup| {
                try global_setup(self.alloc);
            }
            print("Running {s}...\n", .{test_file.name});
            test_file.run(self.alloc) catch |e| {
                print("Failed to run test {s}: {any}\n", .{ test_file.name, e });
                last_error = e;
            };
        }

        if (last_error) |e| {
            return e;
        }
    }
};

pub const TestFile = struct {
    /// Inlined string (`&'static str`). Never deallocated.
    name: []const u8,
    globalSetup: ?*const GlobalSetupFn = null,
    deinit: ?*const GlobalTeardownFn = null,
    run: *const RunFn,

    pub const GlobalSetupFn = fn (alloc: Allocator) anyerror!void;
    pub const GlobalTeardownFn = fn (alloc: Allocator) void;
    pub const RunFn = fn (alloc: Allocator) anyerror!void;

    fn fromSuite(suite: TestSuite) TestFile {
        const gen = struct {
            fn deinit(_: Allocator) void {
                suite.deinit();
            }
            fn run(_: Allocator) anyerror!void {
                suite.run();
            }
        };
        return TestFile{
            .name = suite.name,
            .run = &gen.run,
            .deinit = &gen.deinit,
        };
    }
};
